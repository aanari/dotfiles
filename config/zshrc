# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Bootstrap zinit (must be before any zinit commands)
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# Load Powerlevel10k theme
zinit ice depth=1
zinit light romkatv/powerlevel10k

[[ -f ~/.fresh/build/shell.sh ]] && . ~/.fresh/build/shell.sh

# zsh-fzf-history-search
zinit ice lucid wait'0'
zinit light joshskidmore/zsh-fzf-history-search

# zinit annexes
zinit light-mode for \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl

# Configure Prezto modules before loading
zstyle ':prezto:*:*' color 'yes'
zstyle ':prezto:module:editor' key-bindings 'vi'
zstyle ':prezto:module:editor' dot-expansion 'yes'

# Disable auto-title in tmux to prevent hostname from appearing
if [[ -n "$TMUX" ]]; then
  zstyle ':prezto:module:terminal' auto-title 'no'
  # Set pane title to current directory in tmux
  autoload -Uz add-zsh-hook
  function set_tmux_pane_title() {
    printf '\033]2;%s\007' "${PWD##*/}"
  }
  # Set it initially and on directory change
  set_tmux_pane_title
  add-zsh-hook chpwd set_tmux_pane_title
else
  zstyle ':prezto:module:terminal' auto-title 'yes'
  zstyle ':prezto:module:terminal:window-title' format '%n@%m: %s'
  zstyle ':prezto:module:terminal:tab-title' format '%m: %s'
fi

# Cache completion initialization
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.cache/zsh/completions
# Fuzzy completion matching
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 'r:|=*' 'l:|=* r:|=*'
# Faster git completion
zstyle ':completion:*:*:git:*' script ~/.cache/zsh/git-completion.bash

# Enhanced history settings
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_FIND_NO_DUPS
setopt HIST_VERIFY
setopt INC_APPEND_HISTORY_TIME

# Load Prezto modules with zinit for better integration
zinit snippet PZTM::environment
# Don't load terminal module in tmux - it sets pane titles to hostname
[[ -z "$TMUX" ]] && zinit snippet PZTM::terminal
zinit snippet PZTM::editor
zinit snippet PZTM::history
zinit snippet PZTM::directory
zinit snippet PZTM::utility

# Git module - use built-in git aliases instead of svn
zinit snippet PZTM::git/alias.zsh

# Load history substring search early (before vi-mode)
zinit ice depth=1
zinit light zsh-users/zsh-history-substring-search
zle -N history-substring-search-up
zle -N history-substring-search-down

# Syntax highlighting
zinit ice wait"0" lucid atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay"
zinit light zdharma-continuum/fast-syntax-highlighting

# Autosuggestions
zinit ice wait"0" lucid atload"!_zsh_autosuggest_start"
zinit light zsh-users/zsh-autosuggestions

# Additional completions
zinit ice wait"0" lucid blockf atpull'zinit creinstall -q .'
zinit light zsh-users/zsh-completions

# Plugins
zinit ice wait"0" lucid
zinit pack="bgn-binary+keys" for fzf

zinit ice wait"0" lucid
zinit light Peltoche/lsd

zinit ice wait"0" lucid
zinit light Aloxaf/fzf-tab

# Configure zsh-vi-mode before loading
ZVM_LINE_INIT_MODE=$ZVM_MODE_INSERT
ZVM_VI_INSERT_ESCAPE_BINDKEY=jk

# Load zsh-vi-mode eagerly for immediate cursor feedback
zinit ice depth=1
zinit light jeffreytse/zsh-vi-mode

# Define after_init function to preserve our bindings
zvm_after_init() {
  bindkey -M emacs '^[[A' history-substring-search-up
  bindkey -M emacs '^[[B' history-substring-search-down
  bindkey -M vicmd 'k' history-substring-search-up
  bindkey -M vicmd 'j' history-substring-search-down
  bindkey -M vicmd '^[[A' history-substring-search-up
  bindkey -M vicmd '^[[B' history-substring-search-down
  bindkey -M viins '^[[A' history-substring-search-up
  bindkey -M viins '^[[B' history-substring-search-down
}

# tldr
zinit ice wait"2" as"command" pick"tldr" lucid
zinit light raylee/tldr

# git-cal
zinit wait"2" lucid as"null" \
 atclone'perl Makefile.PL PREFIX=$ZPFX' \
 atpull'%atclone' make sbin"git-cal" for \
    k4rthik/git-cal

zinit wait"1" lucid from"gh-r" as"null" for \
     sbin"**/bat"         @sharkdp/bat \
     sbin"**/fd"          @sharkdp/fd \
     sbin"**/hyperfine"   @sharkdp/hyperfine \
     sbin"**/rg"          @BurntSushi/ripgrep \
     sbin"**/delta"       @dandavison/delta \
     sbin"**/dust"        @bootandy/dust \
     sbin"**/just"        @casey/just

# yank, clipboard
zinit ice as"program" pick"yank" make
zinit light mptre/yank

# direnv
#zinit as"program" make'!' atclone'./direnv hook zsh > zhook.zsh' \
#  atpull'%atclone' pick"direnv" src"zhook.zsh" for \
#  direnv/direnv

# fzf-tab configuration
# disable sort when completing `git checkout`
zstyle ':completion:*:git-checkout:*' sort false
# set descriptions format to enable group support
zstyle ':completion:*:descriptions' format '[%d]'
# set list-colors to enable filename colorizing
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
# preview directory's content with lsd when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'lsd -l -F --group-directories-first --color always --header --icon always --blocks permission,name  --size short $realpath | head -100'
# switch group using `,` and `.`
zstyle ':fzf-tab:*' switch-group ',' '.'


# dircolors
test -r ~/.dir_colors && eval $(dircolors ~/.dir_colors)

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# WSL specific things
if grep --quiet microsoft /proc/version 2>/dev/null; then
  # Set Windows display for WSL
  export DISPLAY=$(cat /etc/resolv.conf | grep nameserver | awk '{print $2; exit;}')':0.0'
  export LIBGL_ALWAYS_INDIRECT=1
fi

# powerlevel10k
# fzf
source ~/.fzf.sh

# Misc
export GIT_PAGER="delta"
alias cat="bat"
alias ll='ls -alh'
alias la='ls -a'
alias ls="lsd"
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=15'
# Configure cursor shapes for different vim modes
ZVM_INSERT_MODE_CURSOR=$ZVM_CURSOR_BLINKING_BEAM
ZVM_NORMAL_MODE_CURSOR=$ZVM_CURSOR_BLOCK
ZVM_VISUAL_MODE_CURSOR=$ZVM_CURSOR_BLOCK
ZVM_VISUAL_LINE_MODE_CURSOR=$ZVM_CURSOR_BLOCK
ZVM_OPPEND_MODE_CURSOR=$ZVM_CURSOR_UNDERLINE

export EDITOR='nvim'
unsetopt PATH_DIRS

# Additional vi mode settings
export KEYTIMEOUT=1  # Reduce delay when switching modes

# direnv hook
eval "$(direnv hook zsh)"

# zoxide init
eval "$(zoxide init zsh)"

# Compile zcompdump, if modified, to increase startup speed.
zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
if [[ -f "$zcompdump" && "$zcompdump" -nt /usr/share/zsh ]] && ! [[ -f "$zcompdump.zwc" ]] || [[ "$zcompdump" -nt "$zcompdump.zwc" ]]; then
  zcompile "$zcompdump"
fi
